NB EQU P1.0
NA EQU P1.1
EV1 EQU P1.4
EV2 EQU P1.5
ESTADO EQU 0x20
EVENTO EQU 0x21

ORG 0x00
	AJMP INICIO

ORG 0x7B
INICIO:
	ACALL INIT
MAIN:
	ACALL FSM
	AJMP MAIN
FSM:
	MOV A, ESTADO
	RL A
	MOV DPTR, #EST_TAB
	JMP @A+DPTR
EST_TAB:
	AJMP ESTADO0
	AJMP ESTADO1
	AJMP ESTADO2
;====================INIT=========================;
INIT:
	MOV ESTADO, #0
	MOV EVENTO, #0
	ANL P1, #00000011b	;Para simulacion!!!
	SETB NA			;Configurar el puerto como entrada
	SETB NB
	SETB EV1		;Configuracion de la electrovalvula
	RET
;===================ESTADO0=======================;
ESTADO0:
	ACALL ES0_GEN_EV
	MOV A, EVENTO
	RL A
	MOV DPTR, #ES0_EV_TAB
	JMP @A+DPTR
ES0_EV_TAB:
	AJMP ES0_EV0
	AJMP ES0_EV1
ES0_EV0:			;EVENTO VACIO
	RET
ES0_EV1:			;EVENTO IR A NIVEL MEDIO
	MOV ESTADO, #1
	SETB EV2			;ABRIMOS LA VALVULA DE ABAJO
	MOV EVENTO, #0
	RET
ES0_GEN_EV: 			;GENERADOR DE EVENTOS
	MOV A, P1
	ANL A, #00000001b
	JNZ ES0_NB_ACTIVADO 		; SI NB se activa nos vamos a nivel medio
	RET
ES0_NB_ACTIVADO:		;CONDICION
	MOV EVENTO, #1
	RET
;===================ESTADO1=======================;
ESTADO1:
	ACALL ES1_GEN_EV
	MOV A, EVENTO
	RL A
	MOV DPTR, #ES1_EV_TAB
	JMP @A+DPTR
ES1_EV_TAB:
	AJMP ES1_EV0
	AJMP ES1_EV1
	AJMP ES1_EV2
ES1_EV0:			;EVENTO VACIO
	RET
ES1_EV1:			;EVENTO IR A NIVEL BAJO
	MOV ESTADO, #0
	CLR EV2				;CERRAMOS LA VALVULA DE ABAJO
	MOV EVENTO, #0
	RET
ES1_EV2:			;EVENTO IR A NIVEL ALTO
	MOV ESTADO, #2
	CLR EV1 			;CERRAMOS LA VALVULA DE ARRIBA
	MOV EVENTO, #0
	RET
ES1_GEN_EV:			;GENERADOR DE EVENTOS
	MOV A, P1
	ANL A, #00000001b
	JZ ES1_NB_DESACTIVADO 		;NIVEL BAJO DESACTIVADO
	MOV A, P1
	ANL A, #00000010b
	JNZ ES1_NA_ACTIVADO 		;NIVEL ALTO ACTIVADO
	RET
ES1_NB_DESACTIVADO:		;CONDICION
	MOV EVENTO, #1
	MOV A, P1
	ANL A, #00000010b 	
	JNZ ERROR			;NIVEL BAJO DESACTIVADO Y BAJO ACTIVADO?!?! ERROR!!!
	RET
ES1_NA_ACTIVADO:
	MOV EVENTO, #2
	RET
;===================ESTADO2=======================;
ESTADO2:
	ACALL ES2_GEN_EV
	MOV A, EVENTO
	RL A
	MOV DPTR, #ES2_EV_TAB
	JMP @A+DPTR
ES2_EV_TAB:
	AJMP ES2_EV0
	AJMP ES2_EV1
ES2_EV0:			;EVENTO VACIO
	RET
ES2_EV1:			;EVENTO IR A NIVEL MEDIO
	MOV ESTADO, #1
	SETB EV1
	MOV EVENTO, #0
	RET
ES2_GEN_EV:			;GENERADOR DE EVENTOS
	MOV A, P1
	ANL A, #00000010b
	JZ ES2_NA_DESACTIVADO
	RET
ES2_NA_DESACTIVADO:		;CONDICION
	MOV EVENTO, #1
	RET
;=====================END=========================;
ERROR:
	;ESCRIBIR 1 EN 7Fh
	MOV @R0, #1
	MOV A, @R0
	MOV 0x7F, A
END


